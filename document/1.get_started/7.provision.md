# Provision
あなたは既にインフラを作成し修正できます。
それでは、provisionersを使って作成時にどのようにインスタンス初期化するかを見ていきましょう。

もし、Packerのようなイメージベースのインフラを使用しているなら、
あなたが学んできたことで十分であるはずだ。
しかし、もしあなたがインスタンスに最初のセットアップを必要とするなら、
provisionersであなたはファイルのアップロード、シェルスクリプトの起動、
インストールしてソフトウェアの起動などを行えます。

## Prerequisites
このトラックでは、以前の章のガイドを読み終えた上で進めてください。
そして、次のような設定でガイドをスタートします。

`learn-terraform-aws-instance`というディレクトリを作成し、
`example.tf`という名前のファイルにコードを貼り付けてください。

```
provider "aws" {
  profile    = "default"
  region     = "us-east-1"
}

resource "aws_instance" "example" {
  ami           = "ami-b374d5a5"
  instance_type = "t2.micro"
}
```

## Defining a Provisioner
provisionerを定義するため、"example"と定義されたEC2インスタンスのリソースブロックを
次のように修正してください。

```
resource "aws_instance" "example" {
  ami           = "ami-b374d5a5"
  instance_type = "t2.micro"

  provisioner "local-exec" {
    command = "echo ${aws_instance.example.public_ip} > ip_address.txt"
  }
}
```

ここでは`resource`のブロックの中で、`provisioner`を追加している。
複数のプロビジョニングステップを定義するために、複数の`provisioner`ブロックを追加できる。
Terraformは複数のプロビジョナーをサポートしているが、この例では`local-exec`のみを使用する。

`terraform init`コマンドと`terraform apply`を実行すると、Terraformの走っているあなたのマシンで
コマンドがプロビジョナーでローカル実行されていることが観測できる。
私たちはこのプロビジョナーを使用しているので、今のところどのような接続の指定も心配する必要が無い。
`local-exec`プロビジョナーで、`terraform apply`コマンドを実行したあなたのローカルマシンで`ip_address.txt`と呼ばれるファイルを表示する。

```
$ cat ip_address.txt
54.89.98.96
```
別の便利なものに`remote-exec`というリモートのリソース内のスクリプトを作成後に起動するプロビジョナーがある。
このコマンドは、構成管理ツールの起動に使うことができ、
クラスターへ組み組むなどの用途が考えられる。
`remote-exec`プロビジョナーを使うためには、sshかwinrmをプロビジョナーブロックのconnectionで接続方法に選ぶ必要がある。
ここでは、どのように`remote-exec`を使い、
単一のインスタンスに特定のパッケージをインストールするかを示す。
以下の例を実行するために適切な権限をもつsshキーを持つ必要がある。

`ssh-keygen -t rsa`でパスフレーズなしのsshキーを作り、
`terraform`という名前にする。
`chmod 400 ~/.ssh/terraform`のコマンドでパーミッションを更新して下さい。

    この例は参考のためのものであり、テストなしで実行すべきでは無い。もしこれを実行するのであれば、新しいTerraformプロジェクトフォルダをこの例のために作る方が良いです。


```
provider "aws" {
  profile = "default"
  region  = "us-west-2"
}

resource "aws_key_pair" "example" {
  key_name   = "examplekey"
  public_key = file("~/.ssh/terraform.pub")
}

resource "aws_instance" "example" {
  key_name      = aws_key_pair.example.key_name
  ami           = "ami-04590e7389a6e577c"
  instance_type = "t2.micro"

  connection {
    type        = "ssh"
    user        = "ec2-user"
    private_key = file("~/.ssh/terraform")
    host        = self.public_ip
  }

  provisioner "remote-exec" {
    inline = [
      "sudo amazon-linux-extras enable nginx1.12",
      "sudo yum -y install nginx",
      "sudo systemctl start nginx"
    ]
  }
}
```

この例では、調べるべきいくつかの部分がある。`aws_key_pair`のリソースはSSH接続に必要である。
キーペアをローカルでAWSにアップロードするために作り、AWSのアップロードを実行する際にキーペアは作られる。
その`aws_instance`リソースは属性値に直接つなぐ`key_name`が必要になる。
`aws_instance`リソースの中で、私たちは接続ブロックの接続タイプ、ユーザ、ホスト、`private_key`の属性を使用する。

`private_key`の属性はホストへのプロビジョンが成功する必要がある。
一度接続が成功した時、この例では`remote-exec`のプロビジョナーはリモートホストでインストールされ、
アップデートをし、nginxを起動する。

## Running Provisioners
プロビジョナーはリソースが作られた時にのみ実行される。
それらは構成管理やすでに実行されているソフトウェアの変更のための置き換えでは無いが、
起動時の初期設定の方法として行われる。
Terraformのプロビジョニングは、実際に構成管理を行うソリューションをトリガーするために使われるべきである。

そして、既に存在するインフラストラクチャーは破壊されることを確認してください。
その上でapplyの実行をして下さい。

```
$ terraform apply
# ...

aws_instance.example: Creating...
  ami:           "" => "ami-b374d5a5"
  instance_type: "" => "t2.micro"
aws_eip.ip: Creating...
  instance: "" => "i-213f350a"

Apply complete! Resources: 2 added, 0 changed, 0 destroyed.
```

Terraformはプロビジョナーからの全ての出力をコンソールに出力する。
しかし、この例ではアウトプットを表示しない。
しかし、`ip_address.txt`のファイルの中身を見ることで全て正常に動作したか検証できる。

```
$ cat ip_address.txt
54.192.26.128
```

コマンドを打った通り`ip_address.txt`にIPが書かれている。

## Failed Provisioners and Tainted Resources
リソースが正常に作成されたが、プロビジョニングで失敗した時、Terraformはエラーを表示して、リソースを"tainted"
というマークを付ける。
tainted状態のリソースは物理的に作られているが、
プロビジョニングが失敗しているのでそのまま使用するのが安全でない状態である。

あなたが次の実行プランを生成した時、Terraformはプロビジョニングの再実施を同じリソースに対して行おうとしない。
それは、安全である保証がされていないからである。
一方で、Terraformはtainted状態のリソースを削除して新しいリソースを作り、
作成後のプロビジョニングを再実施を試みる。

Terraformは自動的にロールバックを行わないし、失敗が生じた時にリソース適用時に削除を行う。
それは、実行計画と齟齬が生じるからである。
実行計画では、リソースを作成されると表明していながら、削除するということは表明しないから、そのような自動ロールバックなどは行わないのである。
もし、taintedリソースが存在する状態で実行計画を作成したら、
計画にtaintedリソースを削除するという計画が表示される。

### Manually Tainting Resources
マニュアルでリソースを削除して再度作成したい時には、
Terraformはtaint機能をCLIに持っている。
このコマンドはインフラを修正せず、taintedという状態になっているリソースの状態を修正する。
一度、リソースがtaintedとマークされると、次の実行に置いてリソースは削除されて再度生成される。

リソースをマニュアルでtaint状態にするため、次のコマンドを実行する

To taint a resource, use the following command:
```
terraform taint resource.id
```

`resource.id`はリソースブロックの名前を参照し、対象IDをもつリソースをtaint状態にする。
以前に作られたリソースブロックを再度見ると以下のなっている。

```
resource "aws_instance" "example" {
  ami           = "ami-b374d5a5"
  instance_type = "t2.micro"
}
```

正しいリソースとIDを`terraform taint aws_instance.example`のコマンドでtaint状態に変更できる。


## Destroy Provisioners
プロビジョナーはインスタンスを削除する時にのみ実行するように定義できる。
このような設定は、システムのクリーンアップ、データの展開などの役に立つ。

多くのリソースで、ビルトインのクリーンアップメカニズムを可能であれば使用することを推奨する。
しかし、プロビジョナーは必要であれば独自で定義できる。

このスタートアップのガイドで削除時のプロビジョナーの例は取り上げない。
もし必要であれば、プロビジョナードキュメントを参照して下さい。
