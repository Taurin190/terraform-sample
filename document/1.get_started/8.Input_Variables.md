# Input Variables
あなたは既にTerraformで有効な構成を作るナレッジを得られていると思います。
しかし、まだアクセスキーやAMIなどの設定がハードコードされている状態だと思います。
実際にシェアしてバージョン管理をできるように、
パラメーターを用いた構成管理にする必要がある。
このページでは入力変数を使用する方法を紹介する。

## The Initial Configuration
もし最初からチュートリアルを進めてきたなら、
`learn-terraform-aws-instance`という名前のディレクトリを作り、
`example.tf`というファイルの中に以下のコードをペーストしていると思う。

```
provider "aws" {
  profile    = "default"
  region     = "us-east-1"
}

resource "aws_instance" "example" {
  ami           = "ami-b374d5a5"
  instance_type = "t2.micro"
}
```

## Defining Variables
最初に、リージョンを変数に展開する。
`variables.tf`という別のファイルを以下のコンテンツで作成して下さい。

    注意：Terraformはディレクトリないの全ての`.tf`という拡張子のファイルを読み込むため、ファイルの名前はどんな名前でも構いません。

```
variable "region" {
  default = "us-east-1"
}
```

これは`region`の変数をTerraformの構成管理の中で定義するものです。
`default`というのは変数を選択可能にします。
もし、デフォルトが設定されていないと、
このガイドで述べられている一つの変数を設定する必要がある。

## Using Variables in a Configuration
次に、providerの設定を次のように変更する。

```
provider "aws" {
  region = var.region
}
```
これは`region`と名付けられた変数で、`var.`という接頭辞を付ける。
それはTerraformに変数にアクセスし、ここで`region`変数をここで使用することを示している。
その設定で、与えられた変数により、AWS providerを設定する。

## Assigning variables
変数を設定する方法は複数あり、
紹介する順番で変数は選ばれる。

### Command-line flags
あなたは変数を直接、コマンドラインで`-var`フラグで設定できる。
どのTerraformコマンド、例えば`apply`, `plan`, `refresh`などでも使うことができる。

```
$ terraform apply \
  -var 'region=us-east-1'
```

もう一度、変数をこの方法で設定すると設定を保存せず、
コマンドとして実行する度に繰り返し入力しなければならない。

### From a file
変数の値を保持するため、ファイルを作り変数をファイルの中に入れて保存できる。
`terraform.tfvars`という名前のファイルを以下の内容で作成して下さい。

```
region = "us-east-1"
```

Terraformは自動的に今いるディレクトリの`terraform.tfvars`や`*.auto.tfvars`というファイルを自動的に読み込む。
もし、ファイルがこのパターンに当てはまらない名前であれば、
`-var-file`というフラグでファイル名を指定すれば使用できる。
これらのファイルはTerraformの構成設定と同じシンタックス（HCL）を使用する。
これらはJSON形式も適用できる。

私たちは、ユーザ名やパスワードをバージョンコントロールに保存することは推奨しない。
ローカルのファイルで、`secret.tfvars`というファイルで`-var-file`のフラグで読み込むのが良いかと思います。

`-var-file`の引数は一つのコマンドに対して複数呼び出すことができるので、
バージョンコントロールするものとしないものを分けて呼び出すことができます。

```
$ terraform apply \
  -var-file="secret.tfvars" \
  -var-file="production.tfvars"
```

    Tip: これはSTG環境と本番環境を同じ構成管理を使い分けてプロビジョニングする一つの方法です。
    

### From environment variables
Terraformは`TF_VAR_name`という形式の環境変数を探し値を取得する。
例えば、`TF_VAR_region`とすれば、`region`というTerraformの変数にセットすることができる。

    Note: 環境変数は文字列の変数のみ入力することができ、Listやmapタイプは別のメカニズムで投入する必要がある。

### UI input
もし、`terraform apply`を何か変数を特定せずに実行した時、
Terraformは対話形式で値の入力を促す。
これらのあたいは保存されないが、Terraformを使い始める時のワークフローとして有益である。
UI入力でのTerraformを毎日の運用は推奨されない。

    Note: Terraformの0.11バージョンより前は、UI入力は文字列型のみサポートされていて、List, map変数は他のメカニズムを使用して入力する必要があった。Terraform 0.12からは、より複雑な入力も可能になっている。

### Variable defaults
もし値が、上記のどの方法でも与えられなかった場合に、変数はdefaultの値を使用する。
そのあたいが変数として使われる。

## Rich data types
文字列型や数値型は最も良く使われる変数である。
しかし、List（配列）やMap（ハッシュテーブルや辞書）なども同様に使われる。

### Lists
リストは、明示的な方法とそうでない方法の両方で定義される。
```
# []のを使って暗黙的に宣言
variable "cidrs" { default = [] }

# 明示的に'list'と書いて宣言
variable "cidrs" { type = list }
```

`terraform.tfvars`のファイル中の値でもリストを指定できます。
```
cidrs = [ "10.0.0.0/16", "10.1.0.0/16" ]
```

### Maps
Mapはkeyとvalueで構造化されたデータです。

私たちは秘匿性の高い文字列を変数に置き換えてきましたが、
AMIについてはまだハードコーディングしていました。
残念なことに、AMIは使用するリージョンによって変わるものです。
そのため、特定のリージョンで適切なAMIを使用者に入力させるのは一つの選択肢ではあるが、
TerraformではMapを使って行うのがより良い方法である。

Mapは変数によって該当する方法を作る方法である。
ここでは、AMIをMapの中で宣言し`us-west-2`のリージョンをサポートする。

```
variable "amis" {
  type = "map"
  default = {
    "us-east-1" = "ami-b374d5a5"
    "us-west-2" = "ami-4b32be2b"
  }
}
```

変数は明示的にMap型で宣言できます。
もしくは、Mapのdefaultの値を指定することで暗黙的に宣言できる。
上記のデモでは明示的な宣言と暗黙的な宣言の両方を示している。

この`amis`Mapを使用するには、`aws_instance`を編集して、
`var.amis`の値を`var.region`のキーを入れて使用する。

```
resource "aws_instance" "example" {
  ami           = var.amis[var.region]
  instance_type = "t2.micro"
}
```
ここで鍵カッコのキーを入力した表記は、どのように`map`の型で値を取得するかを示した例である。
`[var.region]`という動的に変数で`var.amis`の中で宣言した値を取得する。

静的な値を取得するには`var.amis["us-east-1"]`というハードコードで取得する。

### Assigning maps
Mapの値は`-var`もしくは`-var-file`で設定することも可能で、
以下のように設定できる。
```
$ terraform apply -var 'amis={ us-east-1 = "foo", us-west-2 = "bar" }'
```

    注意: `tfvar`ファイルの中でマップにデータが設定される時でも、変数は明示的もしくは暗黙的に分割してデータを設定する必要がある。

これはファイルからMapのキーを設定している例である。
これは`example.tf`の中で定義する。

```
variable "region" {}
variable "amis" {
  type = "map"
}
```

`terraform.tfvars`ファイルでは以下のように宣言する。

```
amis = {
  "us-east-1" = "ami-abc123"
  "us-west-2" = "ami-def456"
}
```

`aws_instance`の中で`amis`と`region`を作成する。

```
resource "aws_instance" "example" {
  ami           = var.amis[var.region]
  instance_type = "t2.micro"
}
```

以下で、選択されたAMIの`aws_instance`リソースからAMIを読んでください。

```
output "ami" {
  value = aws_instance.example.ami
}
```

コマンドラインでリージョンを選択肢してプロビジョニングして確認してください。

```
$ terraform apply -var region=us-west-2

Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

  ami = ami-def456
```