# Input Variables
あなたは既にTerraformで有効な構成を作るナレッジを得られていると思います。
しかし、まだアクセスキーやAMIなどの設定がハードコードされている状態だと思います。
実際にシェアしてバージョン管理をできるように、
パラメーターを用いた構成管理にする必要がある。
このページでは入力変数を使用する方法を紹介する。

## The Initial Configuration
もし最初からチュートリアルを進めてきたなら、
`learn-terraform-aws-instance`という名前のディレクトリを作り、
`example.tf`というファイルの中に以下のコードをペーストしていると思う。

```
provider "aws" {
  profile    = "default"
  region     = "us-east-1"
}

resource "aws_instance" "example" {
  ami           = "ami-b374d5a5"
  instance_type = "t2.micro"
}
```

## Defining Variables
最初に、リージョンを変数に展開する。
`variables.tf`という別のファイルを以下のコンテンツで作成して下さい。

    注意：Terraformはディレクトリないの全ての`.tf`という拡張子のファイルを読み込むため、ファイルの名前はどんな名前でも構いません。

```
variable "region" {
  default = "us-east-1"
}
```

これは`region`の変数をTerraformの構成管理の中で定義するものです。
`default`というのは変数を選択可能にします。
もし、デフォルトが設定されていないと、
このガイドで述べられている一つの変数を設定する必要がある。

## Using Variables in a Configuration
次に、providerの設定を次のように変更する。

```
provider "aws" {
  region = var.region
}
```
これは`region`と名付けられた変数で、`var.`という接頭辞を付ける。
それはTerraformに変数にアクセスし、ここで`region`変数をここで使用することを示している。
その設定で、与えられた変数により、AWS providerを設定する。

## Assigning variables
変数を設定する方法は複数あり、
紹介する順番で変数は選ばれる。

### Command-line flags
あなたは変数を直接、コマンドラインで`-var`フラグで設定できる。
どのTerraformコマンド、例えば`apply`, `plan`, `refresh`などでも使うことができる。

```
$ terraform apply \
  -var 'region=us-east-1'
```

もう一度、変数をこの方法で設定すると設定を保存せず、
コマンドとして実行する度に繰り返し入力しなければならない。

### From a file
変数の値を保持するため、ファイルを作り変数をファイルの中に入れて保存できる。
`terraform.tfvars`という名前のファイルを以下の内容で作成して下さい。

```
region = "us-east-1"
```

Terraformは自動的に今いるディレクトリの`terraform.tfvars`や`*.auto.tfvars`というファイルを自動的に読み込む。
もし、ファイルがこのパターンに当てはまらない名前であれば、
`-var-file`というフラグでファイル名を指定すれば使用できる。
これらのファイルはTerraformの構成設定と同じシンタックス（HCL）を使用する。
これらはJSON形式も適用できる。

私たちは、ユーザ名やパスワードをバージョンコントロールに保存することは推奨しない。
ローカルのファイルで、`secret.tfvars`というファイルで`-var-file`のフラグで読み込むのが良いかと思います。

`-var-file`の引数は一つのコマンドに対して複数呼び出すことができるので、
バージョンコントロールするものとしないものを分けて呼び出すことができます。

```
$ terraform apply \
  -var-file="secret.tfvars" \
  -var-file="production.tfvars"
```

    Tip: これはSTG環境と本番環境を同じ構成管理を使い分けてプロビジョニングする一つの方法です。
    

### From environment variables
Terraformは`TF_VAR_name`という形式の環境変数を探し値を取得する。
例えば、`TF_VAR_region`とすれば、`region`というTerraformの変数にセットすることができる。

    Note: 環境変数は文字列の変数のみ入力することができ、Listやmapタイプは別のメカニズムで投入する必要がある。

### UI input
もし、`terraform apply`を何か変数を特定せずに実行した時、
Terraformは対話形式で値の入力を促す。
これらのあたいは保存されないが、Terraformを使い始める時のワークフローとして有益である。
UI入力でのTerraformを毎日の運用は推奨されない。

    Note: Terraformの0.11バージョンより前は、UI入力は文字列型のみサポートされていて、List, map変数は他のメカニズムを使用して入力する必要があった。Terraform 0.12からは、より複雑な入力も可能になっている。

### Variable defaults
もし値が、上記のどの方法でも与えられなかった場合に、変数はdefaultの値を使用する。
そのあたいが変数として使われる。

## Rich data types
Strings and numbers are the most commonly used variables, but lists (arrays) and maps (hashtables or dictionaries) can also be used.

### Lists
Lists are defined either explicitly or implicitly.
```
# Declare implicitly by using brackets []
variable "cidrs" { default = [] }

# Declare explicitly with 'list'
variable "cidrs" { type = list }
```

You can specify list values in a terraform.tfvars file.
```
cidrs = [ "10.0.0.0/16", "10.1.0.0/16" ]
```

### Maps
A map is a key/value data structure that can contain other keys and values.

We've replaced our sensitive strings with variables, but we are still hard-coding AMIs. Unfortunately, AMIs are specific to the geographical region in use. One option is to ask the user to input the proper AMI for the region, but Terraform can do better than that with a map.

Maps are a way to create variables that are lookup tables. Let's extract our AMIs into a map and add support for the `us-west-2` region.

```
variable "amis" {
  type = "map"
  default = {
    "us-east-1" = "ami-b374d5a5"
    "us-west-2" = "ami-4b32be2b"
  }
}
```

A variable can be explicitly declared as a map type, or it can be implicitly created by specifying a default value that is a map. The above demonstrates both an explicit type = "map" and an implicit `default = {}`.

To use the `amis` map, edit `aws_instance` to use `var.amis` keyed by `var.region`.

```
resource "aws_instance" "example" {
  ami           = var.amis[var.region]
  instance_type = "t2.micro"
}
```
The square-bracket index notation used here is an example of how the `map` type expression is accessed as a variable, with `[var.region]` referencing the `var.amis` declaration for dynamic lookup.

For a static value lookup, the region could be hard-coded such as `var.amis["us-east-1"]`.

### Assigning maps
Map values can also be set using the `-var` and `-var-file` values.
```
$ terraform apply -var 'amis={ us-east-1 = "foo", us-west-2 = "bar" }'
```

    Note: Even if a map's data is set in a `tfvar` file, the variable must be declared separately with either `type="map"` or `default={}`.

Here is an example of setting a map's keys from a file. This is the variable definition in `example.tf`.

```
variable "region" {}
variable "amis" {
  type = "map"
}
```

You can specify keys in a `terraform.tfvars` file.

```
amis = {
  "us-east-1" = "ami-abc123"
  "us-west-2" = "ami-def456"
}
```

Create an `aws_instance` with the `amis` and `region`.

```
resource "aws_instance" "example" {
  ami           = var.amis[var.region]
  instance_type = "t2.micro"
}
```

Read the selected AMI attribute from the `aws_instance` resource.

```
output "ami" {
  value = aws_instance.example.ami
}
```

Provision it by providing a region on the command line.

```
$ terraform apply -var region=us-west-2

Apply complete! Resources: 0 added, 0 changed, 0 destroyed.

Outputs:

  ami = ami-def456
```